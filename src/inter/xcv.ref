* Copyright 2025 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2025-09-14
* https://github.com/Aleksandr3Bocharov/RefalAB

*====================================================================
* Рефал-функции библиотеки РефалАБ.
*====================================================================

CV  START

    ENTRY Cvb, Cvd
    EXTRN Numb, Symb, Add, Mul, Dr, DrN

    IMPL

Cvb '-' V(D).X +
        = '-' <Cvb0 V.X>
    '+' V(D).X +
        = <Cvb0 V.X>
    E(D).X +
        = <Cvb0 E.X>

Cvb0
    V.X S.D1 S.D2 S.D3 S.D4 S.D5 S.D6 S.D7 S.D8 S.D9 +
        = <Add (<Mul (<Cvb0 V.X>) 1000000000>) +
            <Numb S.D1 S.D2 S.D3 S.D4 S.D5 S.D6 S.D7 S.D8 S.D9>>
    E.X = <Numb E.X>

Cvd '-' V(N).X +
        = '-' <Cvd0 V.X>
    '+' V(N).X +
        = <Cvd0 V.X>
    E(N).X +
        = <Cvd0 E.X>

Cvd0 +
    E.X = <Cvd1 <DrN (E.X) 1000000000>>

Cvd1 +
    (E.Y) +
        = <Symb E.Y>
    E.X (E.Y) +
        = <Cvd0 E.X> <Cvd2 <Symb E.Y>>

Cvd2 +
    S.D1 S.D2 S.D3 S.D4 S.D5 S.D6 S.D7 S.D8 S.D9 +
        = S.D1 S.D2 S.D3 S.D4 S.D5 S.D6 S.D7 S.D8 S.D9
    E.X = <Cvd2 '0' E.X>

    END
