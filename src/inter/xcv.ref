* Copyright 2025 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2025-04-14
* https://github.com/Aleksandr3Bocharov/RefalAB

*====================================================================
* Рефал-функции библиотеки РефалАБ.
*====================================================================

CV  START
    ENTRY Cvb, Cvd
    EXTRN Numb, Symb
    EXTRN Add, Mul, Dr, DrN

Cvb '-' V(D).X +
        = '-' <Cvb0 V.X>
    '+' V(D).X +
        = <Cvb0 V.X>
    E(D).X +
        = <Cvb0 E.X>

Cvb0
    V.X S.1 S.2 S.3 S.4 S.5 S.6 S.7 S.8 S.9 +
        = <Add (<Mul (<Cvb0 V.X>) /59/ /10144256/>) +
        <Numb S.1 S.2 S.3 S.4 S.5 S.6 S.7 S.8 S.9>>
    E.X = <Numb E.X>

Cvd '-' V(N).X +
        = '-' <Cvd0 V.X>
    '+' V(N).X +
        = <Cvd0 V.X>
    E(N).X +
        = <Cvd0 E.X>

Cvd0 +
    E.X = <Cvd1 <DrN (E.X) /59/ /10144256/>>

Cvd1 +
    (E.Y) +
        = <Symb E.Y>
    E.X (E.Y) +
        = <Cvd0 E.X> <Cvd2 <Symb E.Y>>

Cvd2 +
    S.1 S.2 S.3 S.4 S.5 S.6 S.7 S.8 S.9 +
        = S.1 S.2 S.3 S.4 S.5 S.6 S.7 S.8 S.9
    E.X = <Cvd2 '0' E.X>

    END
